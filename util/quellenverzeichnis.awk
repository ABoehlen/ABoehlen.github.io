#!/usr/local/bin/awk -f
#########################################################################################
#
# quellenverzeichnis.awk V1.2 27.03.2022
# Autor: Adrian Boehlen
#
# Script generiert die geordnete Quellenliste aus der originalen, ungeordneten Textdatei.
# Als Trennzeichen ist "$" zu verwenden, andernfalls Zeile 17 anpassen
# Die Reihenfolge ist im verweise.js definiert.
# Der Output erfolgt als HTML-Code, der dann direkt ins gewuenschte Dokument integriert
# werden kann. Weblinks werden beruecksichtigt, muessen aber am Beginn des Textes stehen.
#
#########################################################################################

BEGIN {
  # Feldtrenner in Quellen-Textdokument
  FS = "$";

  # Scriptname ermitteln (siehe https://unix.stackexchange.com/questions/228072/how-to-print-own-script-name-in-mawk)
  getline t < "/proc/self/cmdline";
  split(t, a, "\0");
  scriptname = sprintf("%s", a[3]);

  # Usage ausgeben, wenn Anzahl Argumente nicht stimmt und Programm beenden
  if (ARGC != 2) {
    printf("\n****************************************************\n") > "/dev/stderr";
    printf("     Usage: %s <Quelltextfile>\n", scriptname)             > "/dev/stderr";
    printf("****************************************************\n\n") > "/dev/stderr";
    beende = 1; # um END-Regel zum sofortigen Beenden zu erzwingen
    exit;
  }

  # nachstehend benoetigtes Array mit Nummerierung eintragen
  # dieses muss identisch mit dem betreffenden in verweise.js sein
  ##################################

secrKilliDict["aa"] = "1";
secrKilliDict["ab"] = "2";
secrKilliDict["ac"] = "3";
secrKilliDict["ad"] = "4";

  ##################################
  # Kopie von urspruenglichem Array unter dem einheitlichen Namen "verweise" anlegen
  for (i in secrKilliDict)
    verweise[i] = secrKilliDict[i];
}

{
  # Daten zeilenweise einlesen und in Array quellen ablegen
  quellen[$1] = $2;
}

END {
  # damit END nicht ausgefuehrt wird, falls kein File gelesen wird
  if (beende == 1)
    exit;

  # Nummer von verweise mit Text von quellen verbinden
  for (i in verweise)
    quellen_nr[verweise[i]] = quellen[i];

  #### Daten ausgeben ####
  ele = length(quellen_nr);
  print "<!-- automatically generated by quellenverzeichnis.awk -->";

  for (i = 1; i <= ele; i++) {
    # erste Spalte mit hochgestellter Nummer ausgeben
    printf("%s\n\t%s\n\t\t%s%s%s\n", "<div class=\"Reihe\">", "<div class=\"Zelle_Verw_l\">", "<sup>", i, "</sup>");
    printf("\t%s\n", "</div>");

    # falls Quelle ein Weblink ist...
    if (quellen_nr[i] ~ /^http/) {
      anz = split(quellen_nr[i], weblink, " ");

      # ...Link setzen, damit neues Tab geoeffnet wird
      printf("\t%s\n\t\t%s%s%s%s%s", "<div class=\"Zelle_Verw_r\">", "<a target=\"_blank\" href=\"", weblink[1], "\">", weblink[1], "</a>");
      
      # Falls noch Erlaeuterungen folgen...
      if (anz > 1) {

        # ...Resttext bilden und ausgeben, sowie Tabellenspalte und Zeile abschliessen...
        rest = "";
        delete weblink[1];
        for (j = 2; j <= (anz); j++)
          rest = rest " " weblink[j];
        printf("%s\n", rest);
        printf("\t%s\n", "</div>");
        printf("%s\n", "</div>");
      }

      # ...andernfalls Tabellenspalte und Zeile direkt abschliessen
      else {
        printf("\n\t%s\n", "</div>");
        printf("%s\n", "</div>");
      }
    }
    else {
      # Offlinequellen als normalen Text ausgeben, Tabellenspalte und Zeile abschliessen
      printf("\t%s\n\t\t%s\n", "<div class=\"Zelle_Verw_r\">", quellen_nr[i]);
      printf("\t%s\n", "</div>");
      printf("%s\n", "</div>");
    }
  }
}